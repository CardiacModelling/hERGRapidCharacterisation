[[model]]
author: Chon Lok Lei
name: simple_ikr
desc: """
Myokit version of a simple IKr model.

"""
# Initial values
#membrane.V    = -7.43340057623840950e-2  # clamp
ikr.open = 0
ikr.active = 1


[ikr]
use membrane.V
IKr = g * open * active * (V - rev.EK)
dot(open) = (inf - open) / tau
    inf = k1 * tau
    tau = 1 / (k1 + k2)
    k1 = p1 * exp(p2 * V)
    k2 = p3 * exp(-p4 * V)
dot(active) = (inf - active) / tau
    inf = k4 * tau
    tau = 1 / (k3 + k4)
    k3 = p5 * exp(p6 * V)
    k4 = p7 * exp(-p8 * V)
p1 = 2.26e-4 * 1e3 [1/s]
p2 = 0.0699 * 1e3 [1/V]
p3 = 3.45e-5 * 1e3 [1/s]
p4 = 0.05462 * 1e3 [1/V]
p5 = 0.0873 * 1e3 [1/s]
p6 = 8.91e-3 * 1e3 [1/V]
p7 = 5.15e-3 * 1e3 [1/s]
p8 = 0.03158 * 1e3 [1/V]
g = 0.1524 * 1e3 [pA/V]


[engine]
time = 0 bind time
    in [s]
pace = 0 bind pace


[membrane]
V = engine.pace
    desc: membane potential
    in [mV]


#
# Potassium concentrations
#
[potassium]
Ki = 110 [mM]
#Ki = 125 [mM]  # for iPSC solution
Ko = 4 [mM]
#Ko = 3.75 [mM]  # for iPSC solution

#
# Sodium concentrations
#
[sodium]
Nai = 10 [mM]
#Nao = 150 [mM]
Nao = 97.5 [mM]  # for hERG solution
#Nao = 146.25 [mM]  # for iPSC solution

#
# Calcium concentrations (clamp)
#
[calcium]
Cai = 1e-5 in [mM]
#Cai = 0 [mM]  # for hERG and iPSC solutions
#Cao = 1.2 [mM]
Cao = 2.05 [mM]  # for hERG solution
#Cao = 2.3 [mM]  # for iPSC solution


#
# Reversal parameters
#
[rev]
use phys.RTF
ECa = 0.5 * RTF * log(calcium.Cao / calcium.Cai)
    in [V]
EK = RTF * log(potassium.Ko / potassium.Ki)
    in [V]
EKs = RTF * log((potassium.Ko + PkNa * sodium.Nao) / (potassium.Ki + PkNa * sodium.Nai))
    in [V]
ENa = RTF * log(sodium.Nao / sodium.Nai)
    in [V]
PkNa = 0.03


#
# Physical constants
#
[phys]
R = 8.314472 [J/mol/K]
T = 310 [K]  # 36-37oC (BT)
#T = 297 [K]  # 24oC (RT)
F = 9.64853415e4 [C/mol]
RTF = R * T / F

